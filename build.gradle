plugins {
    id "io.spring.dependency-management" version "1.0.3.RELEASE" apply false
    id "org.asciidoctor.convert" version "1.5.3" apply false
}

allprojects {
    apply plugin: 'idea'
    version = '0.1.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) { options.encoding = 'utf-8' }

    repositories {
        mavenLocal()
//        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "io.spring.platform:platform-bom:Brussels-SR4"
        }

        dependencies {
            dependency "org.projectlombok:lombok:${lombokVersion}"
            dependency "com.google.code.findbugs:annotations:${findbugsVersion}"
            dependency group: 'org.modelmapper', name: 'modelmapper', version: '1.1.0'
            dependency group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'
            dependency('com.athaydes:spock-reports:1.2.12') {
                exclude group: 'org.codehaus.groovy'
                exclude group: 'org.spockframework'
            }

        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok"
        testCompileOnly "org.projectlombok:lombok"
        compileOnly 'com.google.code.findbugs:annotations'
        testCompileOnly 'com.google.code.findbugs:annotations'


        testCompile("org.hamcrest:hamcrest-all:1.3")
        testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.9'
    }

    idea {
        module {
            scopes.COMPILE.plus += [configurations.compileOnly]
        }
    }

    test {
        jacoco {
            excludes = ['**/model/**/Q*']
        }
    }
}

idea {
    project {

        jdkName = '1.8'
        languageLevel = '1.8'

        ipr.withXml { xmlFile ->
            // setup Git root
            xmlFile.asNode().component.find { it.@name == 'VcsDirectoryMappings' }.replaceNode {
                component(name: 'VcsDirectoryMappings') {
                    mapping(directory: "", vcs: "Git")
                    mapping(directory: "\$PROJECT_DIR\$", vcs: 'Git')
                }
            }

            // setup annotationProcessing for lombok
            xmlFile.asNode().component.find {
                it.@name == 'CompilerConfiguration'
            }.annotationProcessing.replaceNode {
                annotationProcessing() {
                    profile(default: "true", name: "Default", enabled: 'true')
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}
